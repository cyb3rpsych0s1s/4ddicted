set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
set dotenv-load
set fallback

mod      := 'Addicted'
source   := absolute_path(join('..', '..', 'audioware'))
tree     := 'mods'

@install TO LOCALE:
    just dir '{{ join(TO, tree, mod) }}';
    just dir '{{ join(TO, tree, mod, LOCALE) }}';
    just dir '{{ join(TO, tree, mod, "vanilla", LOCALE) }}';
    just dcopy '{{ join(source, LOCALE) }}' '{{ join(TO, tree, mod) }}'
    just dcopy '{{ join(source, "vanilla", LOCALE) }}' '{{ join(TO, tree, mod, "vanilla") }}'
    just copy '{{ join(source, "voices." + LOCALE + ".yml") }}' '{{ join(TO, tree, mod, "voices.yml") }}'

@uninstall FROM:
    just trash '{{ join(FROM, tree, mod) }}'

# encode .mp3 back into .wav
[macos]
@encode LOCALE='en-us' OVERWRITE='false':
  folders=('{{ join(source, "vanilla", LOCALE) }}' '{{ join(source, LOCALE, "disheartened") }}' '{{ join(source, LOCALE, "offhanded") }}' '{{ join(source, LOCALE, "pestered") }}' '{{ join(source, LOCALE, "surprised") }}'); \
  for folder in ${folders[@]}; do \
    just encode-folder $folder OVERWRITE='{{OVERWRITE}}'; \
  done

[windows]
@encode LOCALE='en-us' OVERWRITE='false':
  $folders = @('{{ join(source, "vanilla", LOCALE) }}', '{{ join(source, LOCALE, "disheartened") }}', '{{ join(source, LOCALE, "offhanded") }}', '{{ join(source, LOCALE, "pestered") }}', '{{ join(source, LOCALE, "surprised") }}'); \
  foreach ($folder in $folders) { \
    just encode-folder $folder '{{OVERWRITE}}'; \
  }

[macos]
@encode-folder FOLDER OVERWRITE='false':
  for file in `ls {{FOLDER}}`; do \
    if [[ ('{{OVERWRITE}}' != 'false' || ! -f '{{ FOLDER }}'/${file%.mp3}.wav) && $file == *.mp3 ]]; then \
      ffmpeg -i '{{ FOLDER }}'/$file -ar 44100 -sample_fmt s16 -y '{{ FOLDER }}'/${file%.mp3}.wav; \
    fi \
  done

[windows]
@encode-folder FOLDER OVERWRITE='false':
  Dir '{{FOLDER}}' -Recurse -File -Filter "*.mp3" | %{ \
    if ($_.Name -clike "*.mp3") { $from = ".mp3"; $into = ".wav"; } else { $from = ".Mp3"; $into = ".Wav"; } \
    if ((!(Test-Path $_.Fullname.Replace($from,$into) -PathType leaf)) -or ([System.Convert]::ToBoolean('{{OVERWRITE}}'))) { ffmpeg -i $_.Fullname -ar 44100 -sample_fmt s16 -y $_.Fullname.Replace($from,$into); } \
  }
